
// Gioca Jouer consists of a bunch of smaller challenges
// that sum up to a boss game. These will be referred to
// as "microgames" in this file. - pokemonpasta

MICRO_SLEEP  <- 0   // don't move
MICRO_WAVE   <- 1   // taunt
MICRO_HITCH  <- 2   // jump
MICRO_SNEEZE <- 3   // crouch
MICRO_WALK   <- 4   // don't stop moving
MICRO_SWIM   <- 5   // swimming
MICRO_SKI    <- 6   // go left
MICRO_SPRAY  <- 7   // look down and use the spray
MICRO_MACHO  <- 8   // spycrab
MICRO_HORN   <- 9   // use kart horn (medic key)
MICRO_BELL   <- 10  // jump + crouch
MICRO_OKAY   <- 11  // say cheers (c+3)
MICRO_KISS   <- 12  // call medic
MICRO_COMB   <- 13  // disguise
MICRO_WAVE2  <- 14  // taunt
MICRO_WAVE3  <- 15  // re-taunt
MICRO_SUPER  <- 16  // rocket jump
MICRO_RESET  <- 17  // Reset. If we consider "reset" a microgame then we dont have to make a separate reset function, and we get previous OnMicroEnd call for free.

micro <- null       // microgame tracker
min_score <- 16     // minimum score to win

minigame <- Ware_MinigameData();
minigame.name = "Gioca Jouer";
minigame.description = "Gioca Jouer!";
minigame.duration = 140.0;
minigame.end_delay = 1.0;
minigame.location = "boxarena";
minigame.music = "giocajouer";
minigame.start_pass = false;
minigame.no_collisions = true;

local pass_sound = "Player.HitSoundBeepo";
PrecacheScriptSound(pass_sound);

local microgame_info =
[
	// description                   overlay                                              absolute times
	["Don't Move!",                 "hud/tf2ware_ultimate/minigames/dont_move",          22.394, 101.723], // MICRO_SLEEP
	["Taunt!",                      "hud/tf2ware_ultimate/minigames/taunt",              25.671, 103.255], // MICRO_WAVE
	["Jump!",                       "hud/tf2ware_ultimate/minigames/jump",               29.103, 105.039], // MICRO_HITCH
	["Crouch!",                     "hud/tf2ware_ultimate/minigames/crouch",             33.147, 106.812], // MICRO_SNEEZE
	["Move!",                       "hud/tf2ware_ultimate/minigames/move",               36.428, 108.627], // MICRO_WALK
	["Swimming!",                   "hud/tf2ware_ultimate/minigames/gioca_jouer_swim",   40.368, 110.264], // MICRO_SWIM
	["Go Left!",                    "hud/tf2ware_ultimate/minigames/go_left",            43.908, 112.206], // MICRO_SKI
	["Look Down and Hit Spray!",    "hud/tf2ware_ultimate/minigames/gioca_jouer_spray",  47.361, 114.010], // MICRO_SPRAY
	["Spycrab!",                    "hud/tf2ware_ultimate/minigames/spycrab",            50.563, 115.689], // MICRO_MACHO
	["Use Kart Horn! (Left Click)", "hud/tf2ware_ultimate/minigames/gioca_jouer_horn",   53.875, 117.515], // MICRO_HORN
	["Jump + Crouch!",              "hud/tf2ware_ultimate/minigames/gioca_jouer_bell",   58.288, 119.372], // MICRO_BELL
	["Say Cheers! (C+3)",           "hud/tf2ware_ultimate/minigames/gioca_jouer_okay",   61.402, 121.146], // MICRO_OKAY
	["Call Medic!",                 "hud/tf2ware_ultimate/minigames/call_medic",         65.550, 123.182], // MICRO_KISS
	["Disguise!",                   "hud/tf2ware_ultimate/minigames/disguise",           68.572, 124.682], // MICRO_COMB
	["Taunt!",                      "hud/tf2ware_ultimate/minigames/taunt",              72.226, 126.529], // MICRO_WAVE2
	["Re-Taunt!",                   "hud/tf2ware_ultimate/minigames/retaunt",            75.954, 128.775], // MICRO_WAVE3
	["Rocket Jump!",                "hud/tf2ware_ultimate/minigames/rocket_jump",        79.432, 130.411], // MICRO_SUPER
	[null,                          null,                                                82.961, 132.342], // MICRO_RESET
];

function OnStart()
{
	foreach(data in Ware_MinigamePlayers)
	{
		local player = data.player;
		local minidata = Ware_GetPlayerMiniData(player);
		minidata.gj_score <- 0;
		minidata.gj_passed <- false;
	}
	// TODO: incorporate into array somehow?
	GiocaJouer_Countdown(5.43); // first round
	GiocaJouer_Countdown(83.05); // second round
	
	// set a timer for each microgame. each tick of
	// the "clock" ends the previous microgame,
	// increments "micro", and starts the next one.
	foreach (array in microgame_info)
	{
		Ware_CreateTimer(@() GiocaJouer_Clock(), array[2]);
		Ware_CreateTimer(@() GiocaJouer_Clock(), array[3]);
	}
}

function GiocaJouer_Countdown(delay)
{
	local timer = 1;
	Ware_CreateTimer(function()
	{
		if (timer <= 8)
		{
			// count up to 8
			Ware_ShowGlobalScreenOverlay(format("hud/tf2ware_ultimate/countdown_%s", timer.tostring()));
			timer++;
			return 0.489;
		}
		else
		{
			// kill the overlay
			Ware_ShowGlobalScreenOverlay(null);
		}
	}, delay);
}

function GiocaJouer_Clock()
{
	if (micro == null)
		micro = 0;
	else
	{
		OnMicroEnd();
		micro++;
	}
	OnMicroStart();
}

function GiocaJouer_PassPlayer(player, pass)
{
	Ware_GetPlayerMiniData(player).gj_passed = pass;
}

function OnMicroStart()
{
	minigame.description = microgame_info[micro][0];
	Ware_ShowGlobalScreenOverlay(microgame_info[micro][1]);
	
	// if we consider reset a microgame, we dont have to make a separate function
	if (micro == MICRO_RESET)
	{
		micro = null;
		return;
	}
	
	// start passed? and also any microgames that need setup
	foreach(data in Ware_MinigamePlayers)
	{
		local player = data.player;
		if (!IsEntityAlive(player))
			continue;
		
		// put default case outside of switch to avoid repeating on false cases that need other code
		GiocaJouer_PassPlayer(player, false);
		switch (micro)
		{
			case MICRO_SLEEP:
			case MICRO_WALK:
				GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_SWIM:
				player.AddCond(TF_COND_SWIMMING_CURSE);
				break;
			case MICRO_HORN:
				player.AddCond(TF_COND_HALLOWEEN_KART);
				break;
		}
	}
	
	// loadouts. can move to switch if a non-global loadout function is made
	if (micro == MICRO_MACHO || micro == MICRO_COMB)
		Ware_SetGlobalLoadout(TF_CLASS_SPY, "Disguise Kit");
	
	// do this one a minigame early bcuz original did it. otherwise move to MICRO_SUPER
	if (micro == MICRO_WAVE3)
		Ware_SetGlobalLoadout(TF_CLASS_SOLDIER, "Rocket Jumper");
}

function OnUpdate()
{
	// setup for MICRO_SPRAY, inefficient inside the foreach
	local sprayed_players = [];
	if (micro == MICRO_SPRAY)
	{
		for (local can; can = FindByClassname(can, "spraycan");)
		{
			MarkForPurge(can);
			can.KeyValueFromString("classname", "ware_spraycan");
			sprayed_players.append(can.GetOwner());
		}
	}
	
	// microgame rules
	foreach (data in Ware_MinigamePlayers)
	{
		local player = data.player;
		if (!IsEntityAlive(player))
			continue;
		switch (micro)
		{
			// TODO: Delay before game checks on don't move/move? timer used in move.nut may not work since its not the start of a microgame and each of these happen twice
			case MICRO_SLEEP:
				if (player.GetAbsVelocity().Length() > 5.0)
					GiocaJouer_PassPlayer(player, false);
				break;
			case MICRO_WAVE:
			case MICRO_WAVE2: // TODO: Remove taunt cooldown between WAVE2 and WAVE3
			case MICRO_WAVE3:
				if (player.IsTaunting())
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_HITCH:
				if (GetPropBool(player, "m_Shared.m_bJumping"))
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_SNEEZE:
				if (player.GetFlags() & FL_DUCKING)
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_WALK:
				if (player.GetAbsVelocity().Length() < 75.0)
					GiocaJouer_PassPlayer(player, false);
				break;
			case MICRO_SWIM:
				if (player.GetAbsVelocity().Length() > 75.0)
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_SKI:
				if (GetPropInt(player, "m_nButtons") & IN_MOVELEFT)
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_SPRAY:
				if (sprayed_players.find(player) != null)
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_MACHO:
				if ((player.GetFlags() & FL_DUCKING) && (player.EyeAngles().x < -70.0))
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_BELL:
				if (GetPropBool(player, "m_Shared.m_bJumping") && (player.GetFlags() & FL_DUCKING))
					GiocaJouer_PassPlayer(player, true);
				break;
			case MICRO_SUPER:
				if (player.GetOrigin().z > -6800.0)
					GiocaJouer_PassPlayer(player, true);
				break;
		}
	}
}

function OnPlayerHorn(player)
{
	if (micro == MICRO_HORN)
		GiocaJouer_PassPlayer(player, true);
}

function OnPlayerVoiceline(player, voiceline)
{
	if (voiceline in VCD_MAP)
	{
		switch (micro)
		{
			case MICRO_OKAY:
				if (VCD_MAP[voiceline].find(".Cheers") != null)
					GiocaJouer_PassPlayer(player, true);
			case MICRO_KISS:
				if (VCD_MAP[voiceline].find(".Medic") != null)
				GiocaJouer_PassPlayer(player, true);
		}
	}
}

function OnMicroEnd()
{
	foreach(data in Ware_MinigamePlayers)
	{
		local player = data.player;
		if (!IsEntityAlive(player))
			continue;
		local minidata = Ware_GetPlayerMiniData(player);
		
		// specific minigame cleanup
		switch (micro)
		{
			case MICRO_WAVE:
			case MICRO_WAVE2:
			case MICRO_WAVE3:
				player.RemoveCond(TF_COND_TAUNTING);
				break;
			case MICRO_SWIM:
				player.RemoveCond(TF_COND_SWIMMING_CURSE);
				break;
			case MICRO_HORN:
				player.RemoveCond(TF_COND_HALLOWEEN_KART);
				break;
			case MICRO_COMB:
				if (player.InCond(TF_COND_DISGUISED))
				{
					GiocaJouer_PassPlayer(player, true);
					player.RemoveCond(TF_COND_DISGUISED);
				}
				Ware_StripPlayer(player, true);
				break;
			case MICRO_MACHO:
			case MICRO_SUPER:
				Ware_StripPlayer(player, true);
				break;
		}
		if (minidata.gj_passed)
		{
			minidata.gj_score++;
			printl(minidata.gj_score);
			// TODO: move emitsound to when you pass the objective for each microgame.
			// For microgames that start false, play it ONCE when you call GiocaJouer_PassPlayer(player, true);
			// For microgames that start true, keep it here in OnEnd()
			EmitSoundOnClient(pass_sound, player);
		}
	}
}

function OnEnd()
{
	local high_score = 0;
	local winners = [];
	foreach(data in Ware_MinigamePlayers)
	{
		local player = data.player;
		local minidata = Ware_GetPlayerMiniData(player);
		local score = minidata.gj_score;
		
		if (score > high_score)
		{
			high_score = score;
			winners.clear();
			winners.append(player);
			continue;
		}
		
		if (score == high_score)
		{
			winners.append(player);
		}
	}
	if (high_score > min_score)
		foreach(player in winners)
			Ware_PassPlayer(player, true);
}
